&НаКлиенте
Перем КэшОтмеченныеСтроки;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = ГМ_ЕИС.ПолучитьОрганизациюПоУмолчанию();
	
	Период.ДатаНачала = НачалоДня(ТекущаяДата());
	Период.ДатаОкончания = КонецДня(ТекущаяДата());
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЗаполнитьТабличныеЧасти();
	КонецЕсли;
	
	Версия = ГМ_ЕИС.ВерсияИнтеграции();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСписокКонтрактовКлиент(НачалоПериода, КонецПериода)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ЕИС_Клиент.СообщитьИнфо("Укажите организацию");
		Возврат;
	КонецЕсли;
	
	ЕИС_Клиент.ПолучитьСписокКонтрактов( НачалоПериода, КонецПериода, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСписокКонтрактов(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
        
    СтандартныйПериод = Новый СтандартныйПериод();

    ДатаНачала = Период.ДатаНачала;
    ДатаОкончания = Период.ДатаОкончания;
    
    СтандартныйПериод.ДатаНачала = ДатаНачала;
    СтандартныйПериод.ДатаОкончания = ДатаОкончания;
    Диалог.Период = СтандартныйПериод;
    
    ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораПериода",ЭтаФорма);
    
    Диалог.Показать(ОписаниеОповещения);   
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныеЧасти()
	
	Объект.Отгрузки.Очистить();
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("Организация", Организация);
	ПараметрыФункции.Вставить("ВыбКонтрагент", Контрагент);
	ПараметрыФункции.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыФункции.Вставить("ДатаОкончания", Период.ДатаОкончания);

	РезультатЗапроса = ГМ_ЕИС.ПолучитьТзРеализаций( ПараметрыФункции );
	
	Если РезультатЗапроса = Неопределено Тогда
		Возврат;	
	КонецЕсли;

	Объект.Отгрузки.Загрузить(РезультатЗапроса.Выгрузить());
	
	РаскраситьТЧОтгрузки();
	
КонецПроцедуры

&НаСервере
Процедура РаскраситьТЧОтгрузки()

	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
		
		Если ЗначениеЗаполнено(СтрОтгрузки.ДокументОтгрузки)
							И СтрОтгрузки.РасхождениеПоСумме Тогда				
			СтрОтгрузки.Цвет = "КРАСНЫЙ";
			Продолжить;
		КонецЕсли;

		Если СтрОтгрузки.СтатусЗапроса = Перечисления.еисСтатусЗапроса.ОбработанУспешно Тогда
			СтрОтгрузки.Цвет = "ЗЕЛЕНЫЙ";
			Продолжить;
		ИначеЕсли ЗначениеЗаполнено(СтрОтгрузки.СтатусЗапроса) Тогда
			СтрОтгрузки.Цвет = "ЖЕЛТЫЙ";
		КонецЕсли;
							
	КонецЦикла;
	
	ПроверитьСоответствияТоваров();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоответствияТоваров()
	
	тзОтгрузки = Объект.Отгрузки.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Документ,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	СоответствияТоваровЕИС.КонтрактныйТовар КАК КонтрактныйТовар
	|ПОМЕСТИТЬ втСоответствия
	|ИЗ
	|	Документ.&ТаблицаТовары& КАК РеализацияТоваровУслугТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кб99_СоответствияТоваровЕИС КАК СоответствияТоваровЕИС
	|		ПО (РеализацияТоваровУслугТовары.Номенклатура = СоответствияТоваровЕИС.Номенклатура
	|				И РеализацияТоваровУслугТовары.Ссылка.&ТекстПоляКонтракт& = СоответствияТоваровЕИС.КонтрактныйТовар.Владелец)
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка В(&СписокДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСоответствия.Документ
	|ИЗ
	|	втСоответствия КАК втСоответствия
	|ГДЕ
	|	втСоответствия.КонтрактныйТовар ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	втСоответствия.Документ";
	Запрос.УстановитьПараметр("СписокДокументов", тзОтгрузки.ВыгрузитьКолонку("Документ"));
	ГМ_ЕИС.УстановитьПоляЗапросаПоКонфигурации(Запрос.Текст);
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из тзРезультат Цикл
		
		СтрОтгрузки = тзОтгрузки.Найти(Стр.Документ, "Документ");
		Если СтрОтгрузки <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(СтрОтгрузки.Цвет) Тогда 
				СтрОтгрузки.Цвет = "СИНИЙ";
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Объект.Отгрузки.Загрузить(тзОтгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура кнОтметитьВсеДокументы(Команда)
	
	Если Команда.Имя = "кнОтметитьВсеДокументы" Тогда
		ОтметитьДокументыОтгрузки(Истина);	
	ИначеЕсли Команда.Имя = "кнОтметитьНеОтправленные" Тогда
		ОтметитьДокументыОтгрузки(, Истина);
	ИначеЕсли Команда.Имя = "кнОтметитьНеОбработанные" Тогда
		ОтметитьДокументыОтгрузки(, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьДокументыОтгрузки(Все = Ложь, НеОтправленные = Ложь, ВОбработке = Ложь)

	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
		
		СтрОтгрузки.Отметка = Ложь;
		
		Если Все Тогда
			СтрОтгрузки.Отметка = Истина;
		ИначеЕсли НеОтправленные Тогда
			Если Не ЗначениеЗаполнено(СтрОтгрузки.СтатусЗапроса) И ЗначениеЗаполнено(СтрОтгрузки.ДокументОтгрузки) Тогда
				СтрОтгрузки.Отметка = Истина;
			КонецЕсли;
		ИначеЕсли ВОбработке Тогда
			Если СтрОтгрузки.СтатусЗапроса = Перечисления.еисСтатусЗапроса.Обрабатывается Тогда
				СтрОтгрузки.Отметка = Истина;
			КонецЕсли;		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)

	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
		СтрОтгрузки.Отметка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура кнОбновитьСписокДокументов(Команда)
	
	ЗаполнитьТабличныеЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВЕИСКлиент()

	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Организация);
	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);
	
	мсДокументовКОтправке = Новый Массив;
	ОтправленныеДокументы = Новый Массив;
	
	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
	
		Если СтрОтгрузки.Отметка 
				И ЗначениеЗаполнено(СтрОтгрузки.ДокументОтгрузки) Тогда
				
			Если ГМ_ЕИС.ПроверитьДокументыПередОтправкой(СтрОтгрузки.ДокументОтгрузки) Тогда
				мсДокументовКОтправке.Добавить(СтрОтгрузки.ДокументОтгрузки);
			Иначе
				ЕИС_Клиент.СообщитьИнфо("Различается количество строк в документе ["+СтрОтгрузки.ДокументОтгрузки+"] отгрузки и расходной накладной."
					, СтрОтгрузки.ДокументОтгрузки);
		    КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Если мсДокументовКОтправке.Количество() Тогда

		Для Каждого Док Из мсДокументовКОтправке Цикл
			
			ФайлыКОтправке = ГМ_ЕИС.ПодготовитьСписокФайловКОтправке(Док);
			ПрикрепленныеФайлы = Новый Массив;
			Для Каждого стрФайл Из ФайлыКОтправке Цикл
				
				Файл = ЕИС_Клиент.ОтправитьФайлы(Компонента, стрФайл, ПараметрыОрганизации);
				
				Если ЗначениеЗаполнено(Файл.id) Тогда
					ПрикрепленныеФайлы.Добавить(Файл);
				КонецЕсли;
			КонецЦикла;
			
			ДанныеПакета = ГМ_ЕИС.СформироватьПакетЕИС(Ложь, Док, Ложь, ПараметрыОрганизации, ПрикрепленныеФайлы);
			Если Не ДанныеПакета.ОшибкаФормирования Тогда
				Для Каждого Файл Из ДанныеПакета.ФайлыПакета Цикл
					тхт = Новый ТекстовыйДокумент;
					тхт.ДобавитьСтроку(Файл.Запрос);
					тхт.Записать(Файл.ИмяФайла, КодировкаТекста.ANSI);
					Если ПараметрыОрганизации["ВыводитьПодробнуюИнформацию"] Тогда
						ЕИС_Клиент.СообщитьИнфо(""+ТекущаяДата() + " Записан файл: "+Файл.ИмяФайла);
					КонецЕсли;
				КонецЦикла;
				
				ИмяФайлаПакет = ЕИС_Клиент.СохранитьПакет(Компонента, ДанныеПакета);
				
				Если ЗначениеЗаполнено(ИмяФайлаПакет) Тогда
					ЕИС_Клиент.Отправить(Компонента, ПараметрыОрганизации, ИмяФайлаПакет, ДанныеПакета.ApplicationId, Док, Ложь);
					ОтправленныеДокументы.Добавить(Док);
				КонецЕсли;
			Иначе
				ЕИС_Клиент.СообщитьИнфо("При формировании пакета по документу["+Док+"] выявлены ошибки, документ не отправлен!");
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		ЕИС_Клиент.СообщитьИнфо("Не выбраны документы для отправки");
		Возврат;
	КонецЕсли;
	
	Если ОтправленныеДокументы.Количество() Тогда
		ЕИС_Клиент.Пауза(ПараметрыОрганизации["ПаузаСек"]);
		ЕИС_Клиент.ПолучитьОтветПоСписку(ОтправленныеДокументы, Компонента, ПараметрыОрганизации);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура кнОтправитьВЕИС(Команда)
	
	ОчиститьСообщения();

	Состояние("ЕИС: отправка документов", , "Подождите");
    ЗапомнитьОтмеченныеСтроки();
	
	ОтправитьВЕИСКлиент();
	
	Состояние("ЕИС: получение ответа");
	
	ЗаполнитьТабличныеЧасти();
	ВосстановитьОтмеченныеСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтветКлиент()
	
	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Организация);
	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);
	
	мсДокументовКОтправке = Новый Массив;
	
	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
	
		Если СтрОтгрузки.Отметка 
				И ЗначениеЗаполнено(СтрОтгрузки.СтатусЗапроса) Тогда
		
			мсДокументовКОтправке.Добавить(СтрОтгрузки.ДокументОтгрузки);
		
		КонецЕсли;
	
	КонецЦикла;
	
	Если мсДокументовКОтправке.Количество() Тогда
		ЕИС_Клиент.ПолучитьОтветПоСписку( мсДокументовКОтправке, Компонента, ПараметрыОрганизации );
	Иначе
		ЕИС_Клиент.СообщитьИнфо("Не выбраны документы");
		Возврат;
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура кнПолучитьОтвет(Команда)
	
	ОчиститьСообщения();
	Состояние("ЕИС: получить ответ", , "Подождите");
	
	ПолучитьОтветКлиент();
	
	ЗаполнитьТабличныеЧасти();
	
КонецПроцедуры

 &НаКлиенте
Процедура ПолучитьСтатусКлиент()

	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Организация);
	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);
	
	мсДокументовКОтправке = Новый Массив;
	
	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
	
		Если СтрОтгрузки.Отметка 
				И ЗначениеЗаполнено(СтрОтгрузки.ДокументОтгрузки) Тогда
		
			мсДокументовКОтправке.Добавить(СтрОтгрузки.ДокументОтгрузки);
		
		КонецЕсли;
	
	КонецЦикла;
	
	Если мсДокументовКОтправке.Количество() Тогда
		ЕИС_Клиент.ПолучитьСтатусыПоСписку( мсДокументовКОтправке, Компонента, ПараметрыОрганизации );
	Иначе
		ЕИС_Клиент.СообщитьИнфо("Не выбраны документы");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтатусДокумента(Команда)
	
	ОчиститьСообщения();
	Состояние("ЕИС: получить статусы документов", , "Подождите");
	
	ПолучитьСтатусКлиент();
	
	ЗаполнитьТабличныеЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура кнПодписать(Команда)
	
	ЕИС_Клиент.Подписать();
	
КонецПроцедуры

&НаКлиенте
Процедура кнЛичныйКабинет(Команда)
	
	ЕИС_Клиент.ОткрытьЛК();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПериода(ВыбПериод, Параметры) Экспорт

	Если ВыбПериод <> Неопределено Тогда
		
		ОчиститьСообщения();
		
		Состояние("Запрос списка контрактов",,"Подождите...");
        ЗагрузитьСписокКонтрактовКлиент(ВыбПериод.ДатаНачала, ВыбПериод.ДатаОкончания);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыОтгрузки(Команда)
	
	ОчиститьСообщения();
	Состояние("ЕИС: Создание документов отгрузки", , "Подождите...");
	
	ЗапомнитьОтмеченныеСтроки();
	
	мсДокументовКОтправке = Новый Массив;
	
	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
	
		Если СтрОтгрузки.Отметка Тогда
			
			Если ЗначениеЗаполнено(СтрОтгрузки.ДокументОтгрузки) Тогда
				ЕИС_Клиент.СообщитьИнфо("В строке №"+СтрОтгрузки.НомерСтроки+" уже создан документ отгрузки, пропускаем...");
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрОтгрузки.Контракт) Тогда
				ЕИС_Клиент.СообщитьИнфо("В строке №"+СтрОтгрузки.НомерСтроки+" не указан контракт, пропускаем...");
				Продолжить;
			КонецЕсли;
		
			мсДокументовКОтправке.Добавить(СтрОтгрузки.Документ);
		
		КонецЕсли;
	
	КонецЦикла;
	
	Если мсДокументовКОтправке.Количество() Тогда
		ГМ_ЕИС.СоздатьДокументыОтгрузки(мсДокументовКОтправке);	
	Иначе
		ЕИС_Клиент.СообщитьИнфо("Нет документов к оформлению");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТабличныеЧасти();
	ВосстановитьОтмеченныеСтроки();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтгрузкиУдалитьДокНаСервере(ДокСсылка)
	
	Попытка
		
		Если Не ДокСсылка = Документы.ЕИС_ТранспортныйКонтейнер.ПустаяСсылка() Тогда 
			
			ДокОбъект = ДокСсылка.ПОлучитьОбъект();
			ДокОбъект.УстановитьПометкуУдаления(Истина);
			Возврат Истина;
			
		КонецЕсли;
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ОтгрузкиОчисткаОтвет(Ответ, Парам) Экспорт
	
	текСтрока = Элементы.Отгрузки.ТекущиеДанные;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если ОтгрузкиУдалитьДокНаСервере(Парам.ДокСсылка) Тогда
			ЗаполнитьТабличныеЧасти();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузкиДокументОтгрузкиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекСтрока = Элементы.Отгрузки.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекСтрока.ДокументОтгрузки) Тогда 
		
		ДокСсылка = ТекСтрока.ДокументОтгрузки;
		
		Парам = Новый Структура("ДокСсылка", ДокСсылка);
    	Оповещение = Новый ОписаниеОповещения("ОтгрузкиОчисткаОтвет", ЭтаФорма, Парам);	
    	ПоказатьВопрос(Оповещение, "Удалить "+ДокСсылка+" ?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да, "");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонтрактыКлиент()
	
	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Организация);
	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);

	СписокКонтрактов = Новый Массив;
	Для Каждого СтрОтгрузки Из Объект.Отгрузки Цикл
	
		Если СтрОтгрузки.Отметка 
				И ЗначениеЗаполнено(СтрОтгрузки.Контракт) Тогда
		
			СписокКонтрактов.Добавить(СтрОтгрузки.Контракт);
		
		КонецЕсли;
	
	КонецЦикла;
	
	Если СписокКонтрактов.Количество() Тогда
		ЕИС_Клиент.ОбновитьКонтрактыПоСписку(СписокКонтрактов, Компонента, ПараметрыОрганизации);
	Иначе
		ЕИС_Клиент.СообщитьИнфо("Не выбраны контракты");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонтракты(Команда)
	
	ОчиститьСообщения();
	Состояние("ЕИС: Обновление контрактов по списку", , "Подождите...");
	
	ОбновитьКонтрактыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьОтмеченныеСтроки()
	
	КэшОтмеченныеСтроки = Новый Массив;
	
	Для Каждого Стр Из Объект.Отгрузки Цикл
		
		Если Стр.Отметка Тогда	
			КэшОтмеченныеСтроки.Добавить(Стр.Документ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьОтмеченныеСтроки()
	
	Для Каждого Док Из КэшОтмеченныеСтроки Цикл
		
		ОтмеченныеСтроки = Объект.Отгрузки.НайтиСтроки(Новый Структура("Документ", Док));
		Для Каждого Стр Из ОтмеченныеСтроки Цикл
			Стр.Отметка = Истина;
		КонецЦикла;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СервисЗаполнитьОрганизациюВКОнтрактахНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	еисКонтракты.Ссылка
		|ИЗ
		|	Справочник.еисКонтракты КАК еисКонтракты";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СПрОб = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		СПрОб.Организация = Организация;
		СПрОб.Записать();
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СервисЗаполнитьОрганизациюВКОнтрактах(Команда)
	
	ОчиститьСообщения();
	СервисЗаполнитьОрганизациюВКОнтрактахНаСервере();
	Сообщить("Выполнено");
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	МассивОбъектов = Новый Массив;
	Для Каждого Стр Из Объект.Отгрузки Цикл
		Если Стр.Отметка И ЗначениеЗаполнено(Стр.ДокументОтгрузки) Тогда
			МассивОбъектов.Добавить(Стр.ДокументОтгрузки);	
		КонецЕсли;
	КонецЦикла;
	
	Если МассивОбъектов.Количество() Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ЕИС_ТранспортныйКонтейнер", "ЕИС_ТранспортныйКонтейнер", МассивОбъектов, Объект);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбраны документы к печати");	
	КонецЕсли;
	
КонецПроцедуры
