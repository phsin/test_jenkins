// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЕИС_ТранспортныйКонтейнер";
	КомандаПечати.Представление = НСтр("ru = 'Печать'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Картинка = БиблиотекаКартинок.Печать;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЕИС_ТранспортныйКонтейнер") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЕИС_ТранспортныйКонтейнер",
			НСтр("ru = 'Транспортный контейнер ЕИС'"),
			СформироватьПечатнуюФорму(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ЕИС_ТранспортныйКонтейнер.Макет");
			
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФорму(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	Перем ПервыйДокумент, НомерСтрокиНачало, Ошибки;
	
	МодульИнтеграции = ГМ_ЕИС.ПолучитьОбщийМодульИнтеграции();
	Макет = Документы.ЕИС_ТранспортныйКонтейнер.ПолучитьМакет("ПФ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕИС_ТранспортныйКонтейнер.ДокументОснование.Номер КАК ДокументОснованиеНомер,
		|	ЕИС_ТранспортныйКонтейнер.ДокументОснование.Дата КАК ДокументОснованиеДата,
		|	ЕИС_ТранспортныйКонтейнер.Организация КАК Организация,
		|	ЕИС_ТранспортныйКонтейнер.Контрагент КАК Контрагент,
		|	ЕИС_ТранспортныйКонтейнер.ЕИС_Контракт КАК ЕИС_Контракт,
		|	ЕИС_ТранспортныйКонтейнер.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Запас,
		|		Номенклатура.Артикул КАК Код,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Коэффициент КАК Коэффициент,
		|		ЕИС_КонтрактныйТовар КАК ЕИС_КонтрактныйТовар,
		|		Цена КАК ЦенаДок,
		|		Сумма КАК СуммаДок,
		|		СтавкаНДС КАК СтавкаНДС,
		|		СуммаНДС КАК СуммаНДС,
		|		Количество КАК КолВоДок,
		|		ЦенаПоКонтракту КАК ЦенаКонтракт,
		|		ВЫРАЗИТЬ(ЦенаПоКонтракту * Количество * Коэффициент КАК ЧИСЛО(12, 2)) КАК СуммаКонтракт,
		|		Количество * Коэффициент КАК КолвоКОнтракт
		|	) КАК Товары
		|ИЗ
		|	Документ.ЕИС_ТранспортныйКонтейнер КАК ЕИС_ТранспортныйКонтейнер
		|ГДЕ
		|	ЕИС_ТранспортныйКонтейнер.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель = Макет.ПолучитьОбласть("Покупатель");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_кб99_ТранспортныйКонтейнер";
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	
	ПервыйДокумент = Истина;
	
	ВыборкаПоШапке = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаПоШапке.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		СведенияОбОрганизации = МодульИнтеграции.ПолучитьСведенияОбОрганизации(ВыборкаПоШапке.Организация);
		СведенияОбКонтрагенте = МодульИнтеграции.ПолучитьСведенияОбОрганизации(ВыборкаПоШапке.Контрагент);

		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоШапке.ДокументОснованиеНомер, Истина, Истина);
		ПредставлениеОснования = НСтр("ru ='расходная накладная № '") + НомерДокумента + НСтр("ru =' от '") + Формат(ВыборкаПоШапке.ДокументОснованиеДата, "ДЛФ=DD");
		
		ТекстЗаголовка = СтрШаблон("Траснпортный контейнер ЕИС к документу %1", ПредставлениеОснования);
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ПредставлениеПоставщика = МодульИнтеграции.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");
		ОбластьПоставщик.Параметры.ПредставлениеПоставщика = ПредставлениеПоставщика;
		ТабДок.Вывести(ОбластьПоставщик);
		
		ПредставлениеПолучателя = МодульИнтеграции.ОписаниеОрганизации(СведенияОбКонтрагенте, "ПолноеНаименование,ФактическийАдрес,Телефоны");
		ОбластьПокупатель.Параметры.ПредставлениеПолучателя = ПредставлениеПолучателя;
        ТабДок.Вывести(ОбластьПокупатель);
		
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		
		ВыборкаТовары = ВыборкаПоШапке.Товары.Выбрать();
			
		ИтогоКолвоДок = 0;
		ИтогоСуммаДок = 0;
		ИтогоКолвоКонтракт = 0;
		ИтогоСуммаКонтракт = 0;
		
		Пока ВыборкаТовары.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
			ТабДок.Вывести(ОбластьСтрока);
			
			ИтогоКолвоДок = ИтогоКолвоДок + ВыборкаТовары.КолВоДок;
			ИтогоСуммаДок = ИтогоСуммаДок + ВыборкаТовары.СуммаДок;
			ИтогоКолвоКонтракт = ИтогоКолвоКонтракт + ВыборкаТовары.КолвоКОнтракт;
			ИтогоСуммаКонтракт = ИтогоСуммаКонтракт + ВыборкаТовары.СуммаКонтракт;
			
		КонецЦикла;
		
		СтруктураПараметровИтого = Новый Структура("ИтогоКолвоДок, ИтогоСуммаДок, ИтогоКолвоКонтракт, ИтогоСуммаКонтракт", 
			ИтогоКолвоДок, ИтогоСуммаДок, ИтогоКолвоКонтракт, ИтогоСуммаКонтракт);
		ОбластьИтого.Параметры.Заполнить(СтруктураПараметровИтого);
		ТабДок.Вывести(ОбластьИтого);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции