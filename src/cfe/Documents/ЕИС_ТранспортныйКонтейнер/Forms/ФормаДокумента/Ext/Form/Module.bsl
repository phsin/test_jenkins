&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьНадписи();
	Если еисСтатусЗапроса = Перечисления.еисСтатусЗапроса.ОбработанУспешно Тогда
		ТолькоПросмотр = Истина;	
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ОбновитьНадписи()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = ГМ_ЕИС.ПоследнийЗапрос(Объект.Ссылка);
	Если Запрос <> Неопределено Тогда
		ApplicationID = Запрос.ApplicationID;
		еисСтатусЗапроса = Запрос.СтатусЗапроса;
		
		Если ЗначениеЗаполнено( Запрос.Ошибка ) Тогда
			Элементы.ГруппаОшибки.Видимость = Истина;
			Ошибки = Запрос.Ошибка;
		Иначе
			Элементы.ГруппаОшибки.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ИнфоСтатусДокумента = ГМ_ЕИС.СтатусДокументаЕИС(Объект.Ссылка);
	ИдДокЕИС = ИнфоСтатусДокумента.еисИдДокумента;
	СтатусДокумента = ИнфоСтатусДокумента.СтатусДокумента;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВЕИС(Команда)
	
	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Объект.Организация);
	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);
	
	ФайлыКОтправке = ГМ_ЕИС.ПодготовитьСписокФайловКОтправке(Объект.Ссылка);
	ПрикрепленныеФайлы_ = Новый Массив;
	Для Каждого стрФайл Из ФайлыКОтправке Цикл
		
		Файл = ЕИС_Клиент.ОтправитьФайлы(Компонента, стрФайл, ПараметрыОрганизации);
		
		Если ЗначениеЗаполнено(Файл.id) Тогда
			ПрикрепленныеФайлы_.Добавить(Файл);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеПакета = ГМ_ЕИС.СформироватьПакетЕИС(Ложь, Объект.Ссылка, Истина, ПараметрыОрганизации, ПрикрепленныеФайлы_);
	Если Не ДанныеПакета.ОшибкаФормирования Тогда
		Для Каждого Файл Из  ДанныеПакета.ФайлыПакета Цикл
			тхт = Новый ТекстовыйДокумент;
			тхт.ДобавитьСтроку(Файл.Запрос);
			тхт.Записать(Файл.ИмяФайла, КодировкаТекста.ANSI);
			Если ПараметрыОрганизации["ВыводитьПодробнуюИнформацию"] Тогда
				ЕИС_Клиент.СообщитьИнфо(ТекущаяДата() + " Записан файл: "+Файл.ИмяФайла);
			КонецЕсли;
		КонецЦикла;
		
		ИмяФайлаПакет = ЕИС_Клиент.СохранитьПакет(Компонента, ДанныеПакета);
		
		Если ЗначениеЗаполнено(ИмяФайлаПакет) Тогда
			ЕИС_Клиент.Отправить(Компонента, ПараметрыОрганизации, ИмяФайлаПакет, ДанныеПакета.ApplicationId, Объект.Ссылка, Ложь);
		КонецЕсли;
	Иначе
		ЕИС_Клиент.СообщитьИнфо("При формировании пакета по документу["+Объект.Ссылка+"] выявлены ошибки, документ не отправлен!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтветЕИС(Команда)
	
	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Объект.Организация);

	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);
	
	ОчиститьСообщения();
	ЕИС_Клиент.ПолучитьОтвет(Компонента, 0, Объект.Ссылка, , ПараметрыОрганизации);
	
	ОбновитьНадписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗапрос(Команда)
	
	ЕИС_Клиент.ОткрытьЗапросЕИС(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтвет(Команда)
	
	ЕИС_Клиент.ОткрытьОтветЕИС(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Модифицированность = Ложь;
	Записать();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	Модифицированность = Ложь;
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕИС_ТекущийЭтапНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеОтбора = Новый Структура("Владелец", Объект.ЕИС_Контракт);
	ПараметрыПодбора = Новый Структура("ЗакрыватьПриВыборе, РежимВыбора, Отбор", Истина, Истина, ЗначениеОтбора);	
	ОткрытьФорму("Справочник.еисЭтапы.ФормаВыбора", ПараметрыПодбора, Элемент);	

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусДокумента(Команда)
		
	ПараметрыОрганизации = ГМ_ЕИС.ПолучитьСтруктуруПараметров(Объект.Организация);

	Компонента = ЕИС_Клиент.ИнициализацияКомпоненты(ПараметрыОрганизации);
	
	ОчиститьСообщения();
	ЕИС_Клиент.ПолучитьДанныеПоДокументу(Объект.Ссылка, Компонента, ПараметрыОрганизации);
	
	ОбновитьНадписи();
	
КонецПроцедуры

&НаСервере
Функция ТоварыЕИС_КонтрактныйТоварПриИзмененииНаСервере(КонтрактныйТовар, Номенклатура)
	
	Если ЗначениеЗаполнено(КонтрактныйТовар) И ЗначениеЗаполнено(Номенклатура) Тогда
		КодТовараЕИС = ?(ЗначениеЗаполнено(КонтрактныйТовар.ОКПД2Код), 
				КонтрактныйТовар.ОКПД2Код, КонтрактныйТовар.КТРУКод);
				
		ГМ_ЕИС.ЗаписатьСоответствиеТоваров(Номенклатура, КонтрактныйТовар);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтрактныйТовар) Тогда
		ЦенаПоКонтракту = КонтрактныйТовар.Цена * 
			?(КонтрактныйТовар.КоличПотребЕдВПотребУпак = 0, 1, КонтрактныйТовар.КоличПотребЕдВПотребУпак);
	Иначе
		ЦенаПоКонтракту = 0;
	КонецЕсли;
	
	Возврат ЦенаПоКонтракту;
	
КонецФункции

&НаКлиенте
Процедура ТоварыЕИС_КонтрактныйТоварПриИзменении(Элемент)
	
	ТекСтр = Элементы.Товары.ТекущиеДанные;
	Номенклатура = ?(ЗначениеЗаполнено(ТекСтр.Содержание), ТекСтр.Содержание, ТекСтр.Номенклатура);
	ТекСтр.ЦенаПоКонтракту = ТоварыЕИС_КонтрактныйТоварПриИзмененииНаСервере(ТекСтр.ЕИС_КонтрактныйТовар, Номенклатура) * ТекСтр.Коэффициент;
    ТекСтр.СуммаПоКонтракту = ТекСтр.ЦенаПоКонтракту * ТекСтр.Количество * ТекСтр.Коэффициент;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	
	РежимДиалога = РежимДиалогаВыбораФайла.Открытие;
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалога);
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	ДиалогВыбораФайла.МножественныйВыбор = Истина;
	ДиалогВыбораФайла.Заголовок = Нстр("ru = 'Выберите файл'");
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		Для Каждого Файл Из ДиалогВыбораФайла.ВыбранныеФайлы Цикл
			НайденныеСтроки = Объект.ПрикрепленныеФайлы.НайтиСтроки(Новый Структура("Путь", Файл));
			Если Не НайденныеСтроки.Количество() Тогда
				нСтр = Объект.ПрикрепленныеФайлы.Добавить();
				нСтр.Путь = Файл;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьНеСопоставленные(Команда)
	
	Элементы.Товары.ОтборСтрок = Неопределено;
	
	УстановленОтборТовары = Не УстановленОтборТовары;
	Если УстановленОтборТовары Тогда
		ЗначениеОтбора = Новый ФиксированнаяСтруктура("ЕИС_КонтрактныйТовар", ПредопределенноеЗначение("Справочник.еисКонтрактныеТовары.ПустаяСсылка"));
		Элементы.ТоварыОтобразитьНеСопоставленные.Заголовок = "Показать все";
	Иначе
		ЗначениеОтбора = Неопределено;
		Элементы.ТоварыОтобразитьНеСопоставленные.Заголовок = "Показать не сопоставленные";
	КонецЕсли;
	
	Элементы.Товары.ОтборСтрок = ЗначениеОтбора;
	
КонецПроцедуры
