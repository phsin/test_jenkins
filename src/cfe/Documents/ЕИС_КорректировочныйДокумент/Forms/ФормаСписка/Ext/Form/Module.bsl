&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = ГМ_ЕИС.ПолучитьОрганизациюПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСписокДокументов(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
        
    СтандартныйПериод = Новый СтандартныйПериод();

    ДатаНачала = НачалоМесяца(ТекущаяДата());
    ДатаОкончания = КонецМесяца(ТекущаяДата());
    
    СтандартныйПериод.ДатаНачала = ДатаНачала;
    СтандартныйПериод.ДатаОкончания = ДатаОкончания;
    Диалог.Период = СтандартныйПериод;
    
    ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораПериода", ЭтаФорма);
    
    Диалог.Показать(ОписаниеОповещения); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПериода(ВыбПериод, Параметры) Экспорт

	Если ВыбПериод <> Неопределено Тогда
		
		ОчиститьСообщения();
		
		Состояние("Запрос списка Корректировочных документов", , "Подождите...");
        ЗагрузитьСписокКорректировокКлиент(ВыбПериод.ДатаНачала, ВыбПериод.ДатаОкончания);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборДинамическогоСписка()
	
	ОтборДинамическогоСписка = Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	ОтборДинамическогоСписка.Элементы.Очистить();
	
	ЭлементОтбора = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Организация;
	ЭлементОтбора.Использование = ЗначениеЗаполнено(Организация);
		
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСписокКорректировокКлиент(НачалоПериода, КонецПериода)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ЕИС_Клиент.СообщитьИнфо("Укажите организацию");
		Возврат;
	КонецЕсли;
	
	ЕИС_Клиент.ПолучитьСписокКорректировок(НачалоПериода, КонецПериода, Организация);
	
КонецПроцедуры