
&НаСервере
Процедура кб99_ЕИС_ПересчитатьСуммуПоКонтрактуПослеНаСервере()
	
	Докум = Объект.Ссылка;
	
	МодульИнтеграции = ГМ_ЕИС.ПолучитьОбщийМодульИнтеграции();
	ПоляЗапросаПоКонфигурации = МодульИнтеграции.ПолучитьПредставлениеПолейДокументаВЗапрос();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслугТовары.НомерСтроки КАК НомерСтроки,
		|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ЕИС_ТранспортныйКонтейнерТовары.ЕИС_КонтрактныйТовар КАК ТоварПоКонтракту,
		|	ЕИС_ТранспортныйКонтейнерТовары.ЦенаПоКонтракту КАК ЦенаПоКонтракту
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЕИС_ТранспортныйКонтейнер.Товары КАК ЕИС_ТранспортныйКонтейнерТовары
		|		ПО РеализацияТоваровУслугТовары.Номенклатура = ЕИС_ТранспортныйКонтейнерТовары.Номенклатура
		|			И РеализацияТоваровУслугТовары.Цена = ЕИС_ТранспортныйКонтейнерТовары.Цена
		|			И РеализацияТоваровУслугТовары.Ссылка = ЕИС_ТранспортныйКонтейнерТовары.Ссылка.ДокументОснование
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Докум);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ГМ_ЕИС.СообщитьИнфо("На основании документа " + Докум + " еще не введен документ Транспортный контейнер ЕИС.");	
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		СтрДокум = Объект.Товары[Выборка.НомерСтроки - 1];
		
		ЗначениеСтавкиНДС = МодульИнтеграции.ПолучитьСтавкуНДС(СтрДокум.СтавкаНДС);
		ЦенаБезНДС = Окр(Выборка.ЦенаПоКонтракту / ((100 + ЗначениеСтавкиНДС) / 100), 11, РежимОкругления.Окр15как20);
		
		СуммаСНДСПоКонтракту = Окр(Выборка.ЦенаПоКонтракту * СтрДокум.Количество, 2, РежимОкругления.Окр15как20);
		СуммаБезНДСПоКонтракту = Окр(ЦенаБезНДС * СтрДокум.Количество, 2, РежимОкругления.Окр15как20);
		//СуммаБезНДСПоКонтракту = Окр(СуммаСНДСПоКонтракту / ((100 + ЗначениеСтавкиНДС) / 100), 2, РежимОкругления.Окр15как20);
		СуммаНДСПоКонтракту = СуммаСНДСПоКонтракту - СуммаБезНДСПоКонтракту;
	
		Если СтрДокум.СуммаСНДС <> СуммаСНДСПоКонтракту Тогда
			ГМ_ЕИС.СообщитьИнфо("["+Докум+"] в строке " + Выборка.НомерСтроки + " изменена Сумма, было: " + СтрДокум.СуммаСНДС +
				" стало: " + СуммаСНДСПоКонтракту);
			СтрДокум.СуммаСНДС = СуммаСНДСПоКонтракту;
			
			Если Объект[ПоляЗапросаПоКонфигурации.ТекстПоляЦенаВключаетНДС] Тогда
				СтрДокум.Сумма = СуммаСНДСПоКонтракту;	
			Иначе
				СтрДокум.Сумма = СуммаСНДСПоКонтракту - СуммаНДСПоКонтракту;
			КонецЕсли;
			СтрДокум.СуммаВзаиморасчетов = 0;
			
		КонецЕсли;
		
		Если СтрДокум.СуммаНДС <> СуммаНДСПоКонтракту Тогда
			ГМ_ЕИС.СообщитьИнфо("["+Докум+"] в строке " + Выборка.НомерСтроки + " изменена Сумма НДС, было: " + СтрДокум.СуммаНДС +
				" стало: " + СуммаНДСПоКонтракту);
			СтрДокум.СуммаНДС = СуммаНДСПоКонтракту;
			
			Если Не Объект[ПоляЗапросаПоКонфигурации.ТекстПоляЦенаВключаетНДС] Тогда
				СтрДокум.Сумма = СуммаСНДСПоКонтракту - СуммаНДСПоКонтракту;
			КонецЕсли;
			СтрДокум.СуммаВзаиморасчетов = 0;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура кб99_ЕИС_кб99_ЕИС_ПересчитатьСуммуПоКонтрактуПосле(Команда)
	
	кб99_ЕИС_ПересчитатьСуммуПоКонтрактуПослеНаСервере();
	Модифицированность = Истина;
	
	РассчитатьИтоговыеПоказатели();
	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);
	
КонецПроцедуры
